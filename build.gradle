buildscript {
    ext {
        springBootVersion = '1.4.0.RELEASE'
    }
    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

// plugins
apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'maven'
apply plugin: 'idea'

// build
group = 'edu.pitt.dbmi'
version = '0.3.0'
description = "CCD Annotations API"

jar {
    baseName = 'ccd-annotations'
    version = version
}

// java version
def compatibilityVersion = 1.8
sourceCompatibility = compatibilityVersion
targetCompatibility = compatibilityVersion

// dependencies
def springfoxVersion = '2.5.0'

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}

dependencies {
    // compile time
    compile("edu.pitt.dbmi:ccd-db:0.6.0")
    compile("edu.pitt.dbmi:ccd-oauth2:1.2.0")
    compile("org.springframework.boot:spring-boot-starter-web:${springBootVersion}") {
        // exclude Tomcat in favor of Undertow
        exclude(module: "spring-boot-starter-tomcat")
    }
    compile("org.springframework.boot:spring-boot-starter-undertow:${springBootVersion}")
    compile("org.springframework.boot:spring-boot-starter-hateoas:${springBootVersion}")

    // testing
    testCompile("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")

    // documentation
    compile("io.springfox:springfox-swagger2:${springfoxVersion}")
    compile("io.springfox:springfox-swagger-ui:${springfoxVersion}")
}

// gradle wrapper (./gradlew)
task wrapper(type: Wrapper) {
    gradleVersion = '3.0'
}
