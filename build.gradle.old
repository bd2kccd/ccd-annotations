buildscript {
    ext {
        springBootVersion='1.3.3.RELEASE'
        asciidoctorVersion='1.5.3'
    }
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://repo.spring.io/libs-milestone' }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.asciidoctor:asciidoctor-gradle-plugin:${asciidoctorVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'idea'
apply plugin: 'org.asciidoctor.convert'

jar {
    baseName = 'ccd-annotations'
    version = '0.3.0'
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

def compatibilityVersion = 1.8
sourceCompatibility = compatibilityVersion
targetCompatibility = compatibilityVersion

repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://repo.spring.io/libs-milestone' }
}

configurations {
    compile.exclude module: "spring-boot-starter-tomcat"
}

ext {
    snippetsDir = file('build/generated-snippets')
    springfoxVersion = '2.4.0'
}

dependencyManagement {
    imports {
        ext['spring-restdocs.version'] = '1.1.0.M1'
    }
}

dependencies {
    // compile time
    compile "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    compile "org.springframework.boot:spring-boot-starter-undertow:${springBootVersion}"
    compile project(":ccd-db")
    compile project(":ccd-oauth2")

    // testing
    testCompile "com.jayway.jsonpath:json-path"
    testCompile "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"

    // documentation
    compile "io.springfox:springfox-swagger2:${springfoxVersion}"
    compile "io.springfox:springfox-swagger-ui:${springfoxVersion}"
    testCompile "org.springframework.restdocs:spring-restdocs-mockmvc"
}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    attributes 'snippets': snippetsDir
    inputs.dir snippetsDir
    dependsOn test
}

// gradle wrapper (./gradlew)
task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}
